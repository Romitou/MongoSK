plugins {
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'java'
}


repositories {
    mavenCentral()
    maven { url = 'https://papermc.io/repo/repository/maven-public/' }
    maven { url = 'https://repo.skriptlang.org/releases' }
}

dependencies {
    implementation group: 'org.mongodb', name: 'mongodb-driver-reactivestreams', version: '4.9.0'
    compileOnly group: 'io.papermc.paper', name: 'paper-api', version: '1.19.2-R0.1-SNAPSHOT'
    compileOnly group: 'com.github.SkriptLang', name: 'Skript', version: '2.7.0-beta1'
}

compileJava {
    options.compilerArgs += ['-source', '1.8', '-target', '1.8']
    options.encoding = 'UTF-8'
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

shadowJar {
    archiveClassifier.set('')
    // Remove all classes of dependencies that are not used by the project
    minimize()
    // Relocate dependencies in order to avoid conflicts with other plugins
    relocate 'com.mongodb', 'fr.romitou.mongosk.libs.driver'
    relocate 'org.bson', 'fr.romitou.mongosk.libs.bson'
    relocate 'org.reactivestreams', 'fr.romitou.mongosk.libs.reactivestreams'
    relocate 'reactor', 'fr.romitou.mongosk.libs.reactor'
}
