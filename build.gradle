plugins {
    id 'java'
    id 'com.gradleup.shadow' version '9.0.0-beta4'
}

group 'fr.romitou.mongosk'
version '2.3.3'

repositories {
    mavenCentral()
    maven { url = 'https://repo.destroystokyo.com/repository/maven-public/' }
    maven { url = 'https://repo.papermc.io/repository/maven-public/' }
    maven { url = 'https://jitpack.io' }
}

dependencies {
    implementation group: 'org.mongodb', name: 'mongodb-driver-reactivestreams', version: '5.4.0'
    compileOnly group: 'io.papermc.paper', name: 'paper-api', version: '1.21.4-R0.1-SNAPSHOT'
    compileOnly group: 'com.github.SkriptLang', name: 'Skript', version: '2.11.2', {
        exclude module: 'Vault'
        exclude module: 'worldguard'
        exclude module: 'worldguard-legacy'
    }
}

compileJava {
    options.compilerArgs += ['-source', '1.8', '-target', '1.8']
    options.encoding = 'UTF-8'
}

shadowJar {
    // Remove all classes of dependencies that are not used by the project
    minimize()
    // Relocate dependencies in order to avoid conflicts with other plugins
    relocate 'com.mongodb', 'fr.romitou.mongosk.libs.driver'
    relocate 'org.bson', 'fr.romitou.mongosk.libs.bson'
    relocate 'org.reactivestreams', 'fr.romitou.mongosk.libs.reactivestreams'
    relocate 'reactor', 'fr.romitou.mongosk.libs.reactor'
}
