plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '8.1.0'
}

group 'fr.romitou.mongosk'
version '2.3.1'

repositories {
    mavenCentral()
    maven { url = 'https://repo.destroystokyo.com/repository/maven-public/' }
    maven { url = 'https://papermc.io/repo/repository/maven-public/' }
    maven { url = 'https://jitpack.io' }
}

dependencies {
    implementation group: 'org.mongodb', name: 'mongodb-driver-reactivestreams', version: '4.8.2'
    shadow group: 'io.papermc.paper', name: 'paper-api', version: '1.19.2-R0.1-SNAPSHOT'
    shadow group: 'com.github.SkriptLang', name: 'Skript', version: '2.6.2', { // Well, the 2.6 build failed...
        exclude module: 'Vault'
        exclude module: 'worldguard'
        exclude module: 'worldguard-legacy'
    }
}

compileJava {
    options.compilerArgs += ['-source', '1.8', '-target', '1.8']
    options.encoding = 'UTF-8'
}

shadowJar {
    // Remove all classes of dependencies that are not used by the project
    minimize()
    // Relocate dependencies in order to avoid conflicts with other plugins
    relocate 'com.mongodb', 'fr.romitou.mongosk.libs.driver'
    relocate 'org.bson', 'fr.romitou.mongosk.libs.bson'
    relocate 'org.reactivestreams', 'fr.romitou.mongosk.libs.reactivestreams'
    relocate 'reactor', 'fr.romitou.mongosk.libs.reactor'
}
